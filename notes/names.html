<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
          "http://www.w3.org/TR/html4/strict.dtd">
<html>
  <head>
    <style>
      .code {
      color : #465F91 ;
      background-color: #F5F5F5;
      }
      pre {
      margin-bottom: 4px;
      font-family: monospace;
      background-color: #F5F5F5;
      }
      pre.verbatim, pre.codepre { }
    </style>
    <title>Names</title>
  </head>
  <body>
    <h1>Names</h1>
    <p>An <code class="code">OccName</code> (<code>compiler/basicTypes/OccName.hs</code>) is a basic type, a pairing of a string with a <code class="code">Namespace</code>.
<pre><code class="code">data NameSpace = VarName        -- Variables, including "real" data constructors
               | DataName       -- "Source" data constructors
               | TvName         -- Type variables
               | TcClsName      -- Type constructors and classes; Haskell has them
                                -- in the same name space for now.
               deriving( Eq, Ord )

data OccName = OccName
    { occNameSpace  :: !NameSpace
    , occNameFS     :: !FastString
    }
</code></pre>
A <code class="code">RdrName</code> (<code>compiler/basicTypes/RdrName.hs</code>) is a wrapped <code class="code">OccName</code>.
<pre><code class="code">data RdrName
  = Unqual OccName
  | Qual ModuleName OccName
  | Orig Module OccName
  | Exact Name
  deriving Data
</code></pre><code class="code">Orig</code> and <code class="code">Exact</code> reader names are thrown up by GHC either from generating code by when parsing builtin syntax. <code class="code">Qual</code> (qualified) and <code class="code">Unqual</code> (unqualified) reader names are the more common. <code class="code">Name</code>s (<code>compiler/basicTypes/Name.hs</code>) are unique, unambiguous and contain information about where the named thing comes from.
<pre><code class="code">data NameSort
  = External Module

  | WiredIn Module TyThing BuiltInSyntax
        -- A variant of External, for wired-in things

  | Internal            -- A user-defined Id or TyVar
                        -- defined in the module being compiled

  | System              -- A system-defined Id or TyVar.  Typically the
                        -- OccName is very uninformative (like 's')
            }

-- | A unique, unambiguous name for something, containing information about where
-- that thing originated.
data Name = Name {
                n_sort :: NameSort,     -- What sort of name it is
                n_occ  :: !OccName,     -- Its occurrence name
                n_uniq :: {-# UNPACK #-} !Unique,
                n_loc  :: !SrcSpan      -- Definition site</code></pre>
</p>
<p>The function <code class="code">newTopSrcBinder</code> (<code>compiler/rename/RnEnv.hs</code>) computes a <code class="code">Name</code> from a <code class="code">Located RdrName</code>. It does this via function <code class="code">newGlobalBinder</code> (<code>compiler/iface/IfaceEnv.hs</code>).
<pre><code class="code">do { this_mod <- getModule
   ; traceRn "newTopSrcBinder" (ppr this_mod $$ ppr rdr_name $$ ppr loc)
   ; newGlobalBinder this_mod (rdrNameOcc rdr_name) loc }
</code></pre>The effect of <code class="code">newGlobalBinder</code> is to make a <code class="code">Name</code> for a thing given its module and <code class="code">OccName</code>. It does this by associating a global unique with the <code class="code">OccName</code> in its defining <code>Module</code>'s <code class="code">NameCache</code> (<code>compiler/basicTypes/NameCache.hs</code>) (an <code class="code">OccEnv</code> is a map keyed off <code class="code">OccNames</code>, a <code class="code">ModuleEnv</code> a map keyed off <code class="code">Modules</code>).
<pre class="code"><code>newtype OccEnv a = A (UniqFM a)
  deriving Data

-- | Per-module cache of original 'OccName's given 'Name's
type OrigNameCache   = ModuleEnv (OccEnv Name)

-- | The NameCache makes sure that there is just one Unique assigned for
-- each original name; i.e. (module-name, occ-name) pair and provides
-- something of a lookup mechanism for those names.
data NameCache
 = NameCache {  nsUniqs :: !UniqSupply,
                -- ^ Supply of uniques
                nsNames :: !OrigNameCache
                -- ^ Ensures that one original name gets one unique
   }
</code></pre>The code for <code class="code">newGlobalBinder</code> is,
<pre><code class="code">newGlobalBinder mod occ loc
  = do { name <- updNameCacheTc mod occ $ \name_cache ->
                 allocateGlobalBinder name_cache mod occ loc
       ; traceIf (text "newGlobalBinder" <+>
                  (vcat [ ppr mod <+> ppr occ <+> ppr loc, ppr name]))
       ; return name }
</code></pre> where <code class="code">allocateGlobalBinder</code> (<code>compiler/iface/IfaceEnv.hs</code>) has this signature
<pre><code class="code">allocateGlobalBinder
  :: NameCache
  -> Module -> OccName -> SrcSpan
  -> (NameCache, Name)
</code></pre> which, in the event of a cache miss, takes a unique from the supply, makes a new name and extends the name cache.
</p>
<h1>Renamer</h1>
    <p>As explained in the <a href="https://ghc.haskell.org/trac/ghc/wiki/Commentary/Compiler/Renamer">GHC compiler commentary</a>, it's the renamer's job to figure out <code class="code">Name</code>s from <code class="code">ReaderName</code>s. The entry point to the renamer is <code class="code">tcRnModuleTcRnM</code> (<code>compiler/typecheck/TcRnDriver.hs</code>) which calls out to <code class="code">tcRnSrcDecls</code> which calls out to <code class="code">rnTopSrcDecls</code> which in turn calls <code class="code">rnSrcDecls</code> (<code>compiler/rename/RnSource.hs</code>) and is where all the action is. The renaming algorithm works in stages, each stage depending on the stages that go before it.
</p>
<h2>Stage 1</h2>
<p>
The first stage is top level binders excluding value bindings (so that is, types, classes, instance declarations, data constructors and record fields). This bit is done by a call to the function <code class="code">getLocalNonValBinders</code> (<code>compiler/rename/RnNames.hs</code>). The work of <code class="code">getLocalNonValBinders</code> itself is split into three parts.
</p><h3>Part 1</h3><p>Here we show the code for the first part, the part that processes type or class declarations.
<pre><code class="code">getLocalNonValBinders fixity_env
     (HsGroup { hs_valds  = binds,
                hs_tyclds = tycl_decls,
                hs_fords  = foreign_decls })
  = do  { -- Process all type/class decls *except* family instances
        ; let inst_decls = tycl_decls >>= group_instds
        ; overload_ok <- xoptM LangExt.DuplicateRecordFields
        ; (tc_avails, tc_fldss)
            <- fmap unzip $ mapM (new_tc overload_ok)
                                 (tyClGroupTyClDecls tycl_decls)
        ; traceRn "getLocalNonValBinders 1" (ppr tc_avails)
        ; envs <- extendGlobalRdrEnvRn tc_avails fixity_env
        ...
</code></pre>In a module that just contains
<pre><code class="code">module Foo where
data R = R {f :: Integer}
</code></pre>function <code class="code">new_tc</code> will only be invoked once.
<pre><code class="code">new_tc :: Bool -> LTyClDecl GhcPs
       -> RnM (AvailInfo, [(Name, [FieldLabel])])
new_tc overload_ok tc_decl -- NOT for type/data instances
    = do { let (bndrs, flds) = hsLTyClDeclBinders tc_decl
         ; names@(main_name : sub_names) <- mapM newTopSrcBinder bndrs
         ; flds' <- mapM (newRecordSelector overload_ok sub_names) flds
         ; let fld_env = case unLoc tc_decl of
                 DataDecl { tcdDataDefn = d } -> mk_fld_env d names flds'
                 _                            -> []
         ; return (AvailTC main_name names flds', fld_env) }
</code></pre><code class="code">hsLtyClDeclBinders tc_decl</code> returns all the binding names of the declaration. The first component represents all binding names except record fields, the second represents field occurrences. The head of the first component is guaranteed to be the name of the declaration. We know of course the action of <code class="code">newTopSrcBinder</code> it maps <code class="code">Located RdrName</code>s to <code class="code">Name</code>s updating the <code class="code">NameCache</code> as it does so. In our case there will be two reader names, the first <code class="code">R</code> corresponding to the type <code class="code">R</code>, the second <code class="code">R</code> corresponding to the data constructor <code class="code">R</code>. You can see this in the renamer trace.
<pre>newTopSrcBinder
  Main
  R
  Foo.hs:4:1-27
newTopSrcBinder
  Main
  R
  Foo.hs:4:10-27
</pre>Fields are processed by <code class="code">newRecordSelector</code> is in source file <code>compiler/rename/RnNames.hs</code>. The argument <code class="code">sub_names</code> is a list of the data constructor <code class="code">Name</code>s. Frankly, all that matters is that list be non-empty - only the head is used (the first data constructor). The non-pathological equation is this.
<pre class="code"><code>newRecordSelector :: Bool -> [Name] -> LFieldOcc GhcPs -> RnM FieldLabel
newRecordSelector overload_ok (dc:_) (L loc (FieldOcc _ (L _ fld)))
  = do { selName <- newTopSrcBinder $ L loc $ field
       ; return $ qualFieldLbl { flSelector = selName } }
  where
    fieldOccName = occNameFS $ rdrNameOcc fld
    qualFieldLbl = mkFieldLabelOccs fieldOccName (nameOccName dc) overload_ok
    field | isExact fld = fld -- Not the common case
                              -- See the source for details.
          | otherwise   = mkRdrUnqual (flSelector qualFieldLbl)
                    </code></pre><code class="code">fld</code> in <code class="code">FieldOcc _ (L _ fld)</code> is a record with a <code class="code">rdrNameFieldOcc</code> of type <code class="code">Located RdrName</code>, <code class="code">rdrNameOcc fld</code> produces the <code class="code">OccName</code> from the <code class="code">RdrName</code>. Remember, <code class="code">OccName</code>s are pairs of a namespace and a string. <code class="code">occNameFS</code> extracts the string so <code class="code">fieldOccName</code> is the just the field label. <code class="code">mkFieldLabelOccs</code> is in <code>basicTypes/FieldLabel.hs</code>
<pre><code class="code">mkFieldLabelOccs :: FieldLabelString -> OccName -> Bool -> FieldLbl OccName
mkFieldLabelOccs lbl dc is_overloaded
  = FieldLabel { flLabel = lbl, flIsOverloaded = is_overloaded
               , flSelector = sel_occ }
  where
    str     = ":" ++ unpackFS lbl ++ ":" ++ occNameString dc
    sel_occ | is_overloaded = mkRecFldSelOcc str
            | otherwise     = mkVarOccFS lbl
</code></pre>The <code class="code">flSelector</code> field here is an <code class="code">OccName</code> in the <code class="code">VarName</code> namespace. The string associated with the <code class="code">OccName</code> can differ from the <code class="code">flLabel</code> string in the event the language extension <code class="code">DuplicteRecordFields</code> is in effect. Mangling is taken care of by <code class="code">mkRecFldSelOcc</code> (<code>compiler/basicTypes/OccName.hs</code>). If overloading were enabled, the field <code class="code">f</code> in <code class="code">R {f :: Integer}</code> would get mangled to <code class="code">$sel:f:R</code>. Returning attention to <code class="code">newRecordSelector</code> it is then apparent that in the normal case, an unqualified reader name is made is of the selector name computed by <code class="code">mkFieldLabelOccs</code>, upgraded to a <code class="code">Name</code> by <code class="code">newTopSrcBinder</code> and a <code class="code">FieldLabel</code> with that name made the result. Of course, on our example, that call to <code class="code">newTopSrcBinder</code> for <code class="code">f</code> can be observed (in this case with <code class="code">DuplicateRecordFields</code> in effect) in the renamer trace too.
<pre>newTopSrcBinder
  Foo
  $sel:f:R
  Foo.hs:4:14
</pre>Going back now to <code class="code">new_tc</code>, the last bit of the procedure reads as follows.
<pre><code class="code">
             ; let fld_env = case unLoc tc_decl of
                     DataDecl { tcdDataDefn = d } -> mk_fld_env d names flds'
                     _                            -> []
</code></pre> That is, if the argument <code class="code">tc_decl</code> under consideration is a data declaration, a <code class="code">fld_env</code> is computed for it by <code class="code">mk_fld_env</code>, a function with this signature.
<pre><code class="code"> mk_fld_env
 :: HsDataDefn GhcPs
    -> [Name]
    -> [FieldLabel]
    -> [(Name, [FieldLabel])]
</code></pre> The role of this function is to compute the mapping from constructor names to fields. Its convenient to be done at this point but otherwise doesn't relate to renaming and so there's no further analysis of this here. Finally, the results of <code class="code">new_tc </code> are returned <code class="code">; return (AvailTC main_name names flds', fld_env) }
</code>, the names harvested as a value of type <code class="code">AvailInfo</code> (<code>compiler/basicTypes/Avail.hs</code>).
<pre><code class="code">-- | Records what things are "available", i.e. in scope
data AvailInfo = Avail Name -- ^ an ordinary identifier in scope
               | AvailTC Name
                         [Name]
                         [FieldLabel]
                                  -- ^ A type or class in scope.
                deriving( Eq, Data )
-- | A collection of 'AvailInfo' - several things that are \"available\"
type Avails = [AvailInfo]
</code></pre> Types and class declarations processed in this way, the first significant part of <code class="code">getLocalNonValBinders</code> is done and the first summary trace <code class="code">traceRn "getLocalNonValBinders 1" (ppr tc_avails)</code> printed. For the running <code class="code">Foo</code> example, it reads like this.
<pre><code class="code">getLocalNonValBinders 1 [Foo.R{Foo.R, Foo.R; f}]</code></pre>Interpreting such a list works like this : each element is an <code class="code">AvailInfo</code> of case <code class="code">AvailTC</code>. In our case,  there is only one corresponding to the type <code class="code">R</code>. <code class="code">R</code> is the name of the type, which comes first then all of the remaining names harvested from the type: the constructor and the field label. The fields are disambiguated from the other type pieces by a '<code>;</code>'.
</p>
<h4><code class="code">GlobalRdrEnv</code></h4>
<p>The next line of interest in <code class="code">getLocalNonValBinders</code> signalling the end of its first part is this.
<pre><code class="code">; envs <- extendGlobalRdrEnvRn tc_avails fixity_env</code></pre>The type <code class="code">TcGblEnv</code> describes the top-level of a module. It is a rather large structure but here the focus is on the embedded <code class="code">GlobalRdrEnv</code> (and <code class="code">FixityEnv</code>) fields. The <code class="code">GlobalRdrEnv</code>is the top-level environment, used during renaming. It is in short a mapping from <code class="code">OccNames</code> to <code class="code">GlobalRdrElt</code> lists.
<pre><code class="code">
-- | Global Reader Environment
type GlobalRdrEnv = OccEnv [GlobalRdrElt]

-- | Global Reader Element
data GlobalRdrElt
  = GRE { gre_name :: Name
        , gre_par  :: Parent
        , gre_lcl :: Bool          -- ^ True <=> the thing was defined locally
        , gre_imp :: [ImportSpec]  -- ^ In scope through these imports
    } deriving (Data, Eq)
</code></pre>With these definitions in hand we can look now to the function <code class="code">extendGlobalRdrEnvRn</code> (<code>compiler/rename/RnNames.hs</code>). Assuming no shadowing (not in a TemplateHaskell "decl. bracket" or GHCi), in essence it reads as follows.
<pre><code class="code">extendGlobalRdrEnvRn avails new_fixities
  = do  { (gbl_env, lcl_env) <- getEnvs
        ; let rdr_env  = tcg_rdr_env gbl_env
              ...
        ; rdr_env2 <- foldM add_gre rdr_env new_gres
        ; let fix_env' = ...
              gbl_env' = gbl_env { tcg_rdr_env = rdr_env2, tcg_fix_env = fix_env' }
        ; traceRn "extendGlobalRdrEnvRn 2" (pprGlobalRdrEnv True rdr_env2)
        ; return (gbl_env', ...) }
    where
     ...
                      </code></pre>So, the workhorses here are <code class="code">new_gres</code> and <code class="code">add_gre</code>. The former, <code class="code">new_gres</code> is defined like this.
<pre><code class="code">new_gres :: [GlobalRdrElt]  -- New LocalDef GREs, derived from avails
new_gres = concatMap localGREsFromAvail avails
</code></pre>The gist of <code class="code">localGREsFromAvail</code>(<code>compiler/basicTypes/RdrName.hs</code>) is the function <code class="code">gresFromAvail</code> with provenance set to <code class="code">Nothing</code>. That of course is absolutely correct for definitions local to the module.
<pre><code class="code">gresFromAvail :: (Name -> Maybe ImportSpec) -> AvailInfo -> [GlobalRdrElt]
gresFromAvail prov_fn avail
  = map mk_gre (availNonFldNames avail) ++ map mk_fld_gre (availFlds avail)
  where
    mk_gre n
      = case prov_fn n of  -- Nothing => bound locally
                           -- Just is => imported from 'is'
          Nothing -> GRE { gre_name = n, gre_par = mkParent n avail
                         , gre_lcl = True, gre_imp = [] }
          Just is -> GRE { gre_name = n, gre_par = mkParent n avail
                         , gre_lcl = False, gre_imp = [is] }

    mk_fld_gre (FieldLabel { flLabel = lbl, flIsOverloaded = is_overloaded
                           , flSelector = n })
      = case prov_fn n of  -- Nothing => bound locally
                           -- Just is => imported from 'is'
          Nothing -> GRE { gre_name = n, gre_par = FldParent (availName avail) mb_lbl
                         , gre_lcl = True, gre_imp = [] }
          Just is -> GRE { gre_name = n, gre_par = FldParent (availName avail) mb_lbl
                         , gre_lcl = False, gre_imp = [is] }
      where
        mb_lbl | is_overloaded = Just lbl
               | otherwise     = Nothing
</code></pre>The definition for <code class="code">mkParent</code> is natural and obvious.
<pre><code class="code">mkParent :: Name -> AvailInfo -> Parent
mkParent _ (Avail _)           = NoParent
mkParent n (AvailTC m _ _) | n == m    = NoParent
                         | otherwise = ParentIs m
</code></pre><code class="code">availName</code> is the main name of the available (that is the type or class).
<pre><code class="code">availName :: AvailInfo -> Name
availName (Avail n)     = n
availName (AvailTC n _ _) = n
</code></pre>The purpose of <code class="code">add_gre</code> of course is to extend the <code class="code">GlobalRdrEnv</code> with a <code class="code">GlobalRdrElt</code>.
<pre><code class="code">add_gre env gre
  | not (null dups)    -- Same OccName defined twice
  = do { addDupDeclErr (gre : dups); return env }

  | otherwise
  = return (extendGlobalRdrEnv env gre)
  where
    name = gre_name gre
    occ  = nameOccName name
    dups = filter isLocalGRE (lookupGlobalRdrEnv env occ)
</code></pre>Before returning, <code class="code">extendGlobalRdrEnvRn</code> outputs the following trace statement.
<pre><code class="code">        ; traceRn "extendGlobalRdrEnvRn 2" (pprGlobalRdrEnv True rdr_env2)
        ; return (gbl_env', lcl_env3) }
</code></pre>This is what that trace looks like on the running example (three elements : the type <code class="code">R</code>, the constructor <code class="code">R</code> and the field with selector name <code class="code">$sel:f:r</code>).
<pre><code class="code">extendGlobalRdrEnvRn 2
  GlobalRdrEnv (locals only) {
    R (unique cESf70):
      Foo.R
        defined at Foo.hs:4:1
    R (unique dESf70):
      Foo.R parent:Foo.R
        defined at Foo.hs:4:10
    $sel:f:R (unique iESf72):
      Foo.$sel:f:R fldparent:Foo.R:Just f
        defined at Foo.hs:4:14 }
</code></pre>
</p>
</p><h3>Part 2</h3><p>In the second of <code class="code">getLocalNonValBinders</code> family instances are processed for any new names they introduce.
<pre><code class="code">getLocalNonValBinders fixity_env
     (HsGroup { hs_valds  = binds,
                hs_tyclds = tycl_decls,
                hs_fords  = foreign_decls })
  = do  { -- Process all type/class decls *except* family instances
        ; let inst_decls = tycl_decls >>= group_instds

         ...

        ; setEnvs envs $ do {
            -- Bring these things into scope first
            -- See Note [Looking up family names in family instances]

          -- Process all family instances
          -- to bring new data constructors into scope
        ; (nti_availss, nti_fldss) <- mapAndUnzipM (new_assoc overload_ok)
                                                   inst_decls
        ...
</code></pre>Analogous to <code class="code">new_tc</code> of the previous section, there exists <code class="code">new_assoc</code> invoked on each instance declaration. <code class="code">new_assoc</code> splits into three cases : type family instances, data family instances and class instances. Type families don't introduce new names.
<pre><code class="code">new_assoc :: Bool -> LInstDecl GhcPs
          -> RnM ([AvailInfo], [(Name, [FieldLabel])])
new_assoc _ (L _ (TyFamInstD {})) = return ([], [])
</code></pre> Data family instances on the other hand do.
<pre><code class="code">new_assoc overload_ok (L _ (DataFamInstD _ d))
  = do { (avail, flds) <- new_di overload_ok Nothing d
       ; return ([avail], flds) }
</code></pre> Harvesting the names introduced by the data family instance is handled by the function <code class="code">new_di</code>.
<pre><code class="code">new_di :: Bool -> Maybe Name -> DataFamInstDecl GhcPs
               -> RnM (AvailInfo, [(Name, [FieldLabel])])
new_di overload_ok mb_cls dfid@(DataFamInstDecl { dfid_eqn =
                                 HsIB { hsib_body = ti_decl }})
    = do { main_name <- lookupFamInstName mb_cls (feqn_tycon ti_decl)
         ; let (bndrs, flds) = hsDataFamInstBinders dfid
         ; sub_names <- mapM newTopSrcBinder bndrs
         ; flds' <- mapM (newRecordSelector overload_ok sub_names) flds
         ; let avail    = AvailTC (unLoc main_name) sub_names flds'
                              -- main_name is not bound here!
               fld_env  = mk_fld_env (feqn_rhs ti_decl) sub_names flds'
         ; return (avail, fld_env) }
</code></pre>The last case is for class instances. Class instance declarations can introduce new data family instances.
<pre><code class="code">new_assoc overload_ok
        (L _ (ClsInstD _ (ClsInstDecl { cid_poly_ty = inst_ty
                                      , cid_datafam_insts = adts })))
  | Just (L loc cls_rdr) <- getLHsInstDeclClass_maybe inst_ty
  = do { cls_nm <- setSrcSpan loc $ lookupGlobalOccRn cls_rdr
       ; (avails, fldss)
                <- mapAndUnzipM (new_loc_di overload_ok (Just cls_nm)) adts
       ; return (avails, concat fldss) }
  | otherwise
  = return ([], [])    -- Do not crash on ill-formed instances
                       -- Eg   instance !Show Int   Trac #3811c
</code></pre>In this case, the available info and fields generated by the class
instance declaration are harvested by the function <code class="code">new_loc_di</code> a thin wrapper over the already seen <code class="code">new_di</code>.
<pre><code class="code">new_loc_di :: Bool -> Maybe Name -> LDataFamInstDecl GhcPs
               -> RnM (AvailInfo, [(Name, [FieldLabel])])
new_loc_di overload_ok mb_cls (L _ d) = new_di overload_ok mb_cls d
</code></pre>Suppose the running example were extended to contain the following definition.
<pre><code class="code">instance Show R where
    show (R{f=x}) = "R {f = " ++ show x ++ "}"
</code></pre> The call to <code class="code">lookupGlobalOccRn</code> in <code class="code">new_assoc</code> searches for the class name <code class="code">Show</code> in the global reader env. As the <a href="">Compiler Commentary</a> tells us, the global rdr-env is created before the renamer descends into the top-level bindings ofa module. Before we get to processing local definitions, the global rdr-env contains <code class="code">Name</code>s for all imported toplevel binders. We skipped this so far. The relevant function is <code class="code">importsFromLocalDecls</code> (<code>compiler/rename/RnNames.hs</code>). When the class name is found in the global reader env, the function <code class="code">addUsedGRE</code> (<code>compiler/rename/RnEnv.hs</code>) is called on the global reader element. This records uses of any imported GREs.
<pre><code class="code">addUsedGREs :: [GlobalRdrElt] -> RnM ()
addUsedGREs gres
  | null imp_gres = return ()
  | otherwise     = do { env <- getGblEnv
                       ; traceRn "addUsedGREs" (ppr imp_gres)
                       ; updMutVar (tcg_used_gres env) (imp_gres ++) }
  where
    imp_gres = filterOut isLocalGRE gres
</code></pre>You can see this in the renamer trace.
<pre>addUsedGRE
  Show
    imported from ‘Prelude’ at /Users/shaynefletcher/project/da.git/scratch/shayne/no-record-selector-tests/Reactor1.hs:2:8-10
    (and originally defined in ‘GHC.Show’)
</pre>
</p>
<h3>Part 3</h3>
<p>The last part of <code class="code">getLocalNonValBinders</code> is to find names relating to foreign import declarations and pattern synonyms.
<pre><code class="code">          -- Finish off with value binders:
          --    foreign decls and pattern synonyms for an ordinary module
          --    type sigs in case of a hs-boot file only
        ; is_boot <- tcIsHsBootOrSig
        ; let val_bndrs | is_boot   = hs_boot_sig_bndrs
                        | otherwise = for_hs_bndrs
        ; val_avails <- mapM new_simple val_bndrs

        ; let avails    = concat nti_availss ++ val_avails
              new_bndrs = availsToNameSetWithSelectors avails `unionNameSet`
                          availsToNameSetWithSelectors tc_avails
              flds      = concat nti_fldss ++ concat tc_fldss
        ; traceRn "getLocalNonValBinders 2" (ppr avails)
        ; (tcg_env, tcl_env) <- extendGlobalRdrEnvRn avails fixity_env

        -- Extend tcg_field_env with new fields (this used to be the
        -- work of extendRecordFieldEnv)
        ; let field_env = extendNameEnvList (tcg_field_env tcg_env) flds
              envs      = (tcg_env { tcg_field_env = field_env }, tcl_env)

        ; traceRn "getLocalNonValBinders 3" (vcat [ppr flds, ppr field_env])
        ; return (envs, new_bndrs) } }
</code></pre>Our example program not containing any new names that would come up here, the trace at this point reads like this.
<pre>
  getLocalNonValBinders 2 []
  extendGlobalRdrEnvRn 2
  GlobalRdrEnv (locals only) {
    R (unique cESf70):
      R defined at /Users/shaynefletcher/project/da.git/scratch/shayne/no-record-selector-tests/Reactor1.hs:4:1
    R (unique dESf70):
      R parent:R
        defined at /Users/shaynefletcher/project/da.git/scratch/shayne/no-record-selector-tests/Reactor1.hs:4:10
    $sel:f:R (unique iESf72):
      Foo.$sel:f:R fldparent:R:Just f
        defined at /Users/shaynefletcher/project/da.git/scratch/shayne/no-record-selector-tests/Reactor1.hs:4:14 }
getLocalNonValBinders 3
  [(R, [f{Foo.$sel:f:R}])]
  [r2 :-> [f{Foo.$sel:f:R}]]
</pre>In that last trace we are getting a view on the <code class="code">tcg_field_env</code> in the <code class="code">TcGlobalEnv</code> (the mapping of field names to constructors, the production of which we glossed over above).
</p>
<h2>Stage 2</h2>
<p>Stage 2 starts by harvesting names from pattern synonyms. This is denoted (D1) in <code class="code">rnSrcDecls</code> <code class="code">(compiler/rename/RnSource.hs)</code>. We'll jump straight past this and got to (D2) renaming the left-hand sides of value bindings. This is peformed b y the function <code class="code">rnTopBindsLHS</code> (<code class="code">compiler/rename/RnBinds.hs</code>)
</p>
  </body>
</html>
